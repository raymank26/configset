buildscript {

    def localProperties = {
        Properties properties = new Properties()
        def file = project.rootProject.file('local.properties')
        if (file.exists()) {
            properties.load(file.newDataInputStream())
        } else {
            logger.info("Unable to find file 'local.properties' in project directory")
        }
        properties
    }()

    ext {
        kotlin_version = "$kotlinVersion"
        mavenUrl = localProperties.getProperty("mavenUrl")
        mavenLogin = localProperties.getProperty("mavenLogin")
        mavenPassword = localProperties.getProperty("mavenPassword")
    }
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id("org.jmailen.kotlinter") version "3.7.0"
}

subprojects {
    apply plugin: 'kotlin'
    apply plugin: 'maven-publish'
    apply plugin: 'org.jmailen.kotlinter'

    repositories {
        mavenLocal()
        mavenCentral()
    }

    kotlinter {
        disabledRules = ['indent']
    }
}
