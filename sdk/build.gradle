plugins {
    id "com.google.protobuf" version "0.8.17"
    id 'maven-publish'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    api group: 'com.google.protobuf', name: 'protobuf-java', version: '3.10.0'
    api group: 'io.grpc', name: 'grpc-all', version: '1.25.0'
    api 'org.slf4j:slf4j-api:1.7.21'
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation "com.auth0:java-jwt:$authZeroVersion"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
    implementation "javax.annotation:javax.annotation-api:1.3.2"

    testImplementation 'junit:junit:4.12'
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjvm-default=all']
    }
}

sourceSets {
    main {
        java {
            srcDirs 'build/generated/source/proto/main/grpc'
            srcDirs 'build/generated/source/proto/main/java'
        }
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.1'
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.25.0'
        }
    }

    generateProtoTasks {
        all()*.plugins {
            grpc {}
        }
    }
}

targetCompatibility = "1.8"
sourceCompatibility = "1.8"

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.configset'
            artifactId = 'configset-sdk'
            from components.java
        }
    }

    repositories {
        maven {
            url mavenUrl
            credentials {
                username mavenLogin
                password mavenPassword
            }
        }
    }
}

version = "0.0.1"

