plugins {
    id 'maven-publish'
}

java {
    registerFeature('tomlConfig') {
        usingSourceSet(sourceSets.main)
    }
    registerFeature('grpcConfig') {
        usingSourceSet(sourceSets.main)
    }
}

dependencies {
    api project(':common-client')
    implementation "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"

    grpcConfigImplementation(project(":common-client")) {
        capabilities {
            requireCapability("configset:common-client-grpc")
        }
    }
    tomlConfigImplementation "com.fasterxml.jackson.dataformat:jackson-dataformat-toml:$jacksonDataformatVersion"

    testImplementation(testFixtures(project(":server")))
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"
    testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
    testImplementation "org.amshove.kluent:kluent:$kluentVersion"
    testImplementation "org.awaitility:awaitility:$awaitilityVersion"
    testImplementation group: 'io.mockk', name: 'mockk', version: "$mockkVersion"
    testImplementation group: 'ch.qos.logback', name: 'logback-classic', version: "$logbackVersion"
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ['-Xjvm-default=all']
    }

}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs = ['-Xjvm-default=all']
    }
}

targetCompatibility = "1.8"
sourceCompatibility = "1.8"

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.configset'
            artifactId = 'configset-client'
            from components.java
        }
    }
}

tasks.withType(Test) {
    useJUnitPlatform()
}
