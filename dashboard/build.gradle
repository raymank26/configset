plugins {
    id 'com.bmuschko.docker-java-application' version '6.4.0'
    id 'com.bmuschko.docker-remote-api' version '6.4.0'
    id 'java-test-fixtures'
    id 'org.unbroken-dome.test-sets' version '4.0.0'
}


import com.bmuschko.gradle.docker.tasks.image.DockerBuildImage
import com.bmuschko.gradle.docker.tasks.image.Dockerfile

apply plugin: 'application'

mainClassName = "com.configset.dashboard.Main"

testSets {
    functionalTest
}

dependencies {
    implementation project(':sdk')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation group: 'ch.qos.logback', name: 'logback-classic', version: "$logbackVersion"
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: "$jacksonVersion"
    implementation group: "com.fasterxml.jackson.dataformat", name: 'jackson-dataformat-xml', version: "$jacksonDataformatVersion"
    implementation "javax.annotation:javax.annotation-api:$javaxAnnotationVersion"
    implementation "io.javalin:javalin:$javalinVersion"
    implementation "com.squareup.okhttp3:okhttp:$okHttpVersion"
    implementation group: 'commons-codec', name: 'commons-codec', version: "$commonsCodecVersion"
    implementation "com.hubspot.jinjava:jinjava:$jinjavaVersion"
    implementation "com.google.guava:guava:$guavaVersion"
    implementation "com.auth0:java-jwt:$authZeroVersion"

    testImplementation(testFixtures(project(":server")))
    testImplementation "org.awaitility:awaitility:$awaitilityVersion"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$jupiterVersion"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"

    testImplementation group: 'io.mockk', name: 'mockk', version: "$mockkVersion"
    testImplementation "org.testcontainers:testcontainers:$testcontainersVersion"
    testImplementation "org.testcontainers:postgresql:$testcontainersPostgresVersion"
    testImplementation "com.github.tomakehurst:wiremock-jre8:$wiremockVersion"

    testImplementation "org.amshove.kluent:kluent:$kluentVersion"
    testImplementation "io.arrow-kt:arrow-core:$arrowCore"
    testImplementation "com.codeborne:selenide:$selenideVersion"
    testImplementation "com.codeborne:selenide-proxy:$selenideVersion"
}

run {
    jvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=0.0.0.0:5004"]
}

application {
    applicationDefaultJvmArgs = ["-Xdebug", "-Xrunjdwp:transport=dt_socket,server=y,suspend=n,address=0.0.0.0:5555"]
}


version = "0.0.3"

//dev
task createDevDockerfile(type: Dockerfile) {
    destFile = project.file("$buildDir/docker-dev/Dockerfile")
    from 'openjdk:12'
    addFile("dashboard", "/dashboard")
    entryPoint "/dashboard/bin/dashboard"
    exposePort(8188)
}

task dockerPrepareDevCtx(type: Copy) {
    dependsOn ("installDist", "createDevDockerfile")
    from "$buildDir/install/dashboard"
    into "$buildDir/docker-dev/dashboard"
}

task dockerBuildDevImage(type: DockerBuildImage) {
    inputDir = project.file("$buildDir/docker-dev")
    dependsOn ("dockerPrepareDevCtx")
    images.set(["configset-dashboard-dev:${version}", 'configset-dashboard-dev:latest'])
}

// default
task dockerCreateDefaulfile(type: Dockerfile) {
    destFile = project.file("$buildDir/docker-default/Dockerfile")
    from 'openjdk:12'
    addFile("dashboard", "/dashboard")
    entryPoint "/dashboard/bin/dashboard"
    exposePort(8188)
    environmentVariable("serve.static", "true")
}

task buildFrontend {
    doLast {
        exec {
            workingDir "frontend"
            commandLine = ["npm", "run", "build"]
        }
    }
}

task dockerPrepareDefaultCtx {
    dependsOn ("dockerCreateDefaulfile", "installDist", "buildFrontend")
    doLast {
        project.copy {
            from "$buildDir/install/dashboard"
            into "$buildDir/docker-default/dashboard"
        }
    }
    doLast {
        project.copy {
            from "frontend/dist"
            into "$buildDir/docker-default/dashboard/frontend"
        }
    }
}

task dockerBuildDefaultImage(type: DockerBuildImage) {
    inputDir = project.file("$buildDir/docker-default")
    dependsOn "dockerPrepareDefaultCtx"
    images.set(["configset-dashboard:${version}", 'configset-dashboard:latest'])
}

tasks.withType(Test) {
    useJUnitPlatform()
}

check.dependsOn functionalTest

