plugins {
    id "com.google.protobuf" version "0.8.17"
    id 'maven-publish'
}

sourceSets {
    grpc {
        kotlin {
            srcDir 'src/grpc/kotlin'
        }
        java {
            srcDirs 'build/generated/source/proto/grpc/java'
            srcDir 'build/generated/source/proto/grpc/grpc'
        }
    }
}

java {
    registerFeature('grpc') {
        usingSourceSet(sourceSets.grpc)
    }
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    grpcApi group: 'com.google.protobuf', name: 'protobuf-java', version: '3.10.0'
    grpcApi group: 'io.grpc', name: 'grpc-all', version: '1.25.0'
    grpcImplementation 'javax.annotation:javax.annotation-api:1.3.1'

    api "org.slf4j:slf4j-api:$slf4jVersion"
    implementation "javax.annotation:javax.annotation-api:1.3.2"
}

compileKotlin {
    kotlinOptions {
        freeCompilerArgs = ['-Xjvm-default=all']
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.10.1'
    }

    plugins {
        grpc {
            artifact = 'io.grpc:protoc-gen-grpc-java:1.25.0'
        }
    }

    generateProtoTasks {
        ofSourceSet('grpc').configureEach {
            plugins {
                // Apply the "grpc" plugin whose spec is defined above, without
                // options.  Note the braces cannot be omitted, otherwise the
                // plugin will not be added. This is because of the implicit way
                // NamedDomainObjectContainer binds the methods.
                grpc {}
            }
        }
    }
}

sourceCompatibility = "$javaVersion"
targetCompatibility = "$javaVersion"

java {
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = 'com.configset'
            artifactId = 'configset-common-client'
            from components.java
        }
    }
}

